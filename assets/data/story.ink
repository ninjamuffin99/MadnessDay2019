MADNESS GAME LMAO ->title
==title
+ [START GAME] ->auditor_shit
+ [CREDITS]
    MADE BY NINJAMUFFIN99 AND CYMBOURINE FOR MADNESS DAY 2019
    ART BY CYMBOURINE, ADDITIONAL BACKGROUND ART HELP BY BRANDYBUIZEL
    PROGRAMMING AND WRITING BY NINJAMUFFIN99
    MADE WITH HAXEFLIXEL AND INK BY INKLE STUDIOS
    ->title

==auditor_shit==
    You get out of some test tubes or something.
    You walk up to a computer with a chat app open.
    {|There's a daed body lying on the floor| There's two bodies lying on the ground in front of the computer. They both seem to be daed...| There's a group of dead bodies on the floor... all dead...}
    :BIGBADAUD999: Are you ready?
    + Yes[]... I think so...
        :BIGBADAUD999: good.... ive had some intel prepped...
        -> choose_route
    + [No]
        Everything goes black.
        ->auditor_shit
    
    =choose_route
    
    :BIGBADAUD999: choose a route...
    * [Hank]            -> hank_park -> choose_route
    * [Gas Station]     -> deimos_sanford -> choose_route
    * [Mall]            -> deimos_sanford_mall ->choose_route
    - Finished game??
    ->END


==start==
You wake up in a lab. 
* [Enter next room]
- You walk up to enter the next room. The automatic door has no trouble sliding up and opening for you, revealing the room behind.
There's a heavy metal table in the room. The room is illuminated by a sole window and the blood red skies, shining light on papers that are on the table.
Right next to the entrance you entered, there's a single locker that's ajar, with it's padlock laying on the floor.

* (locker) [Locker]
    You open up the locker.
    Inside there's nothing but a few papers. You skim through them. They're too technical for you to understand, they all seem to be some reports of some sort. 
    * * However there's a paper that stand out from the rest. 
    - - A photo of 2 people. One with a headset and visor, the other with shades and a bandana.
    Who are they?
* [Table]
    You walk up to the large table. There are some office supplies on it. Some pens, a stapler.
    A variety of different papers are scattered across it. Some paper's are reports, some are pictures of different places. 
    On top of the mess of papers though, an image of someone. A picture taken from afar. He has some red goggles, and a bandana over his head and face. Seems like all these reports are about this person...
- 
* [Go to the {locker: table|locker}]
-
BZZZZZZZZZZZZZZZZZ
BZZZZZZZZZZZZZZZZZZZ
Alarms go off around you!
Before you could get to the {locker: table | locker}, what seems like an entire army start rushing into the lab room. You see them investigate the lab shit.
* [Run for the window]
    You try to sneak towards the window, but the guards spot you from the other room.
    One shoots towards you, but miss and actually break the window. Perfect!
    You run towards it, and without giving it a second thought, you dive through.
    You fall a few stories before you hit the ground and blackout. 
    ->streets_intro
* Fight them
- fasdf
->END

==streets_intro==
FADE BACK IN

You start regaining consciousness. 

->END



== hank_park ==
* [You decide to take a stroll to the park.]
- It's a nice red day out today. There are other people hanging around the park.
An overall lovely and nice day.
* [Sit down at a bench]
- 
It's a nice and comfy bench. It's a standard bench made of wood and metal. Nothing too crazy about it.
:????:Yooooooo!
You turn and who else do you see than your good old pal Hank!
:Hank: Hey pal! What's up!
* [You tell Hank your recent shenanigans]
* [The sky]
    :Hank: Haha! Now that was a funny joke!
- :Hank: It's good to see you doing well friend!
:Hank: I was just strolling by for fun and pleasure!
:Hank: And turns out I run into you! Ha! My good friend!
:Hank: What a good and lucky day I am having!
Hank sits down on the bench beside you and opens up his phone.
:Hank: HA! Bro look at this funny meme I found!
Hank pronounced it as "me-me". I'm just letting you know.
Hank shows you his phone. It's an image of a very fat guy, and it says "gimme more borger!"
* [Chuckle at it]
    :Hank: I knew you would like it! 
* [Laugh. HARD.]
    :Hank: I knew you would like it! Haha!
* [No reaction]
    :Hank: It's very funny! I think you just don't get the joke.
    :Hank: Hey it's Hank J. Wimbleton. Here to explain the joke.
    :Hank: Basically it's funny because it is a severly obese Madness grunt depicted in a low quality art style. He is saying 'gimme more borger' because he is hungry and fat people are known to eat a lot of food.
    :Hank: He is overweight, normally Madness grunts are not overweight so this one is different. Another thing that makes it different is the gramatical errors.
    :Hank: Neither 'gimme' nor 'borger' are the correct spelling of either word, and 'gimme' isn't capitalized even though it is the beginning of a setance. That too makes this meme different and odd
    :Hank: it is different thats why its so funny haha do you get it. like if you agree
- :Hank: I sure do love memes a lot!
:Hank: Do you got a meme you like to laugh at?
* [Show Hank a funny wacky meme]
:Hank: Can I see?
You bust your phone out. Praying you don't scroll through certain embarrassing pictures that you don't want Hank seeing, you find a meme.
    #show the WE ARE GOING TO BEAT YOU TO DEATH meme
    :Hank: This one is a good one! Can you explain the joke for me?!
    * * [Hey madness grunt here to explain the joke]
    - - What's funny about this joke is....
    * * [They are actually really nice]
    - -
* [No meme for hank]
    :Hank: Aw man dangit. Oh well! I'll just find some on 9Gag later.
- 
Someone turns on a boombox
it makes hank mad, and he kills people
he feels cool
you go to his place and play fortnite

->->

==deimos_sanford_mall==
    You walk into the mall's entrance. Inside you see a clean hall with stores scattered on each side.
    You were supposed to meet up with them... near the food court it seems.
    * [Check the mall's map]
    - Seems you went into the entrance nearest to the food court. Lucky!
    * You walk to the food court[], and right there is Deimos and Sanford! Deimos is patiently waiting, while Sanford seems to be playing a game on his phone.
    - :Deimos: Yoooo!
    :Sanford: Hey! Aw SHIT!
    :Sanford: Just lost, dangit... Ah well let's roll and see what's happenin around here.
    :Deimos: Hey! I thought we were getting some ice cream first!
    :Deimos: We're in the food court anyways.
    :Sanford: Come on, let's just stroll around for a bit, and we just ate a little snack while we were waiting.
    - (mall_choices)
    * (got_ice_cream) [Get ice cream]
        ->getting_ice_cream
    * (explored) [Explore a bit]
        ->going_exploring
    - ->leave_mall
    
    =getting_ice_cream
        :Deimos: Yeah see, let's get some scoops fellas.
        {not explored: :Sanford: Hnng, alright FINE.}
        :Deimos: Heheheh
        - - (icecream_waiting)
        * * [You all stand in line and wait to order]
        {There's a few people in front of you. You are all gonna have to wait a hot minute for some ice cream |The line progresses slightly forward. Hopefully the icecream isn't melted by the time you get to the front...|Alright, the guy in front of you is ordering...and he's taking forever....for a one scoop plain cone....|->exit_loop_icecream}
        ->icecream_waiting
        //There's probably an easier way to do this.
        =exit_loop_icecream
            :Deimos: Finally!
            :Deimos: I'd like a scoop of vanilla!
            :Sanford: Really? An absolute nothing vanilla flavoured ice cream??
            :Deimos: I'm a simple guy Sanford.
            :Sanford: Whatever, hey hook me up with some rocky road, two scoops of it, in a waffle cone.
            "And you?"
            * * [Vanilla]
            * * [Chocolate]
            * * [Mint]
            * * [Cookies and Cream]
            - - :Deimos: Nice!
            The person working at the shop hands the cones to you all, and Deimos pays for it all.
            The boys and you walk over to sit down at a table when suddenly
            :Deimos: AHH!
            Deimos trips and falls onto you, making a mess of your clothes with his ice cream!
            :Sanford: Deimos! You dunce!
            :Deimos: Aw maaaaaaaan.... my ice cream ;_;
            Deimos looks at you, and realizes that he got your clothes messy with his ice cream
            :Deimos: Woah sorry! Man I feel bad...
            * * [You accept Deimos' apology]
            - - :Deimos: Well damn, now you're gonna have to walk around all day in messy clothes...
            :Deimos: Unless...
            :Deimos: How about we hook you up with some new threads fella?
            :Deimos: We're at the mall anyways!
            You would prefer to walk around in clean clothes...
            * * [You accept the offer]
            - -:Deimos: Hell yeah
            :Sanford: Alright, let's go lookin for the store.
        
            -> clothes_store
    
    =clothes_store
        {explored: You remember the clothes store was right next the video game shop.| After looking for a little while, you fellas find the clothes store.}
        You walk into the clothes store. 
        You look at some shit.
        Deimos and Sanford pick you out a fit, since they stylin and shit.
        ->mall_choices
    
    =going_exploring
        You all walk around the mall for a bit. You see some clothes stores, but what you're really after is the video game shop.
        Deimos looks for some cool ass retro games.
        Actually you have a choice between a few shops.
        Video game shop with deimos, or the arts and crafts store with Sanford. Or something by yourself.
        After you all buy shit, you meet up again and head to the food court, or leave the mall
        -> mall_choices
        
    =leave_mall
        After a fun day you decide to leave the mall.
        Recap what you did. Maybe you hung out with Deimos more.
        ->->



==deimos_sanford==
    You meet up with the boys at the gas station.
    fulpfadein
    fulpsetbg 711.jpg
    You see Sanford outside pumping gas
    fulpactor sanford 50 60
    fulpactor deimos 400 60
    fulphide deimos
    s
    :Sanford: "Ah hello! Good to see you!" 
    He's currently pumping gas into their car. 
    :Sanford: "Deimos is inside getting some snacks.
    * (in_store) [Go inside with Deimos]
        You head inside the gas station.
        fulpsetbg 711Inside.jpg
        fulphide sanford
        fulpactor deimos 400 60
        s
        You see Deimos.
        :Deimos: "Yooo!" 
        Deimos is holding a variety of snacks and sodas. "
        :Deimos: I'm just grabbin' a bunch of snacks. Nab what you want from here, I gotchu.
        Aw sweet. What a nice guy :3
        Well you're in a gas station, your options are a bit limited.
        * * (buy_candy) You get some candy[], specifically a nice chocolate bar and some gummies.
        * * (buy_jerky) You get some beef jerky[]. There's nothin like eatin some jerky with the boys.
        * * (buy_soda) You get a bottle of soda[]. Just some cola will do. As long as that shit is fizzy and roughens your teeth up, it's good enough for you.
        * * (buy_nothing) You get nothing[]. Don't wanna feel like a schmooze when you guys are hanging out for the first time.
        - - You walk up to the counter with Deimos. {buy_nothing: Deimos places his snacks on the counter| You place your {buy_candy: candy}{buy_jerky: jerky}{buy_soda: soda} on the counter along with Deimos' snacks.}
        {not buy_nothing: As promised, Deimos buys you your stuff, what a gentleman. Both of you head out the store. Sanford seems to be finsihing up the fill up too.}
    * (in_car) Stay with Sanford
        :Sanford: "It's a little breezy out today, feel free to hop in the backseat bud."
        You hop in the backseat of the car. There's a little bit of trash in the backseat. Some empty chip bags, candy wrappers, and a half eaten bag of beef jerky.
        Grab some?
        * * (grabbed_jerky) [Yes]
            Eat it?
            * * * (ate_car_jerky) [Yes]
                You take a hard chomp on the piece of jerky. It's VERY chew-y.... Although it still has a deliciously salty taste. A tinge of sweetness too. At one point this was the finest of jerky.
            * * * (saved_car_jerky) [No]
                You put it in your pocket for later. It's likely you'll need it for something more important in the story. You never know what may happen. 
        * * [No]
            You decide not to grab a piece. Maybe it was just a trap.... Maybe it was a test.... Deimos and Sanford are seeing if you trying to sneak some of their secret stash of jerky.... Well, well, well, looks like you may have passed. 
        - - Just as Sanford finishes up the fill up, Deimos comes walking out with a bag of snacks.
    - :Deimos: "Alright, let's bounce!"
    * {not in_car} [You get in the car]
    * ->
    -
    fulpsetbg carinside.jpg
    s
    fulpactor sanford
    fulpactor deimos
    You all get into the car. Deimos in the drivers seat, Sanford riding shotgun. You in the back like a goon.
    {grabbed_jerky && ate_car_jerky: Deimos sniffs. "Why it smell like jerky? "Huh?" Sanford looks to you. He notices the open bag of jerky. He looks back to you. Now back to the bag.}
    {grabbed_jerky && ate_car_jerky: "Aw nice some leftover jerky!" Deimos grabs a handful of jerky. "Want some?"}
    * {grabbed_jerky && ate_car_jerky} [Grab some]
        You grab some jerky too. Even though you already ate some.... At least he offered it this time rather than you STEALING IT.
        :Deimos: "Kinda stale huh?"
        You nod in agreement
    * {grabbed_jerky && ate_car_jerky} "No thanks" 
        :Deimos: "Alright no biggie" 
        Deimos takes a bite 
        :Deimos: "This stuff is a little.... stale..."
    * ->
    - Deimos starts the car and it comes to life. 
    The car gets pulled out of the gas station and into the streets. Not too long later you're all on the open highway, driving through the empty grey plains. 
    Nothing in sight other than some power poles and the baren red skyline.
    
    Sanford seems to turn towards you.
    :Sanford: "So you ever been through this part of the state?"
    * [You tell him about your little travels] 
    - :Sanford: "Interesting, well just you wait until you see Nevada from up in the cliffsides. You can see the whole town from there"
    You're getting a little bored, and hungry too.
    {buy_nothing: Maybe you should have gotten some food... | Good thing Deimos bought some {buy_candy: candy}{buy_jerky: jerky}{buy_soda: soda} for you.}
    * (eating_snacks) {in_store && not buy_nothing} [{in_store && not buy_nothing: You {buy_soda:crack }open your{buy_candy: candy}{buy_jerky: jerky}{buy_soda: soda} and {buy_soda: take a sip | eat some.}}]
        
    * (saved_snacks) {in_store && not buy_nothing} [You save it for later]
        You never know when you'll need some {buy_candy: candy}{buy_jerky: jerky}{buy_soda: soda} handy. 
    * ->
    - 
    Sanford turns the radio on. Some cool ass drum and bass techno ass music starts playing and breaks the somewhat awkward silence. 
    :Deimos: "Hell yeah"
    This music sets the mood nicely. Three homies cruising through Nevada listening to cool ass drum and bass techno ass music. Few words are being spoken, but you feel the bond forming between you three, as you join them on this small little adventure.
    
    Cliffsides off in the distance ahead of the car start moving towards you. It's not too long when the car approaches it.
    The car follows a rough path up the sides. It reaches a spot that overlooks the plains and you can see the town far off in the distance.
    Sanford turns the car off.
    :Sanford: Here we are!
    You guys get out of the car.
    You all look at the sunset.
    There's a conversation between you three.
    You all get back in the car and drive back into town.
->->







